export DISABLE_AUTO_TITLE=true

if [[ `uname -m` == "arm64" ]]; then
  path=(/opt/homebrew/bin /opt/homebrew/sbin $path)
fi

export HOMEBREW_ROOT=$(brew --prefix)
export HOMEBREW_CACHE=$(brew --cache)
export HOMEBREW_NO_INSTALL_CLEANUP=1 # handled in crontab
export HOMEBREW_AUTO_UPDATE_SECS=$((6 * 60 * 60)) # 6 hours

export CFLAGS="-I$HOMEBREW_ROOT/include"
export LDFLAGS="-L$HOMEBREW_ROOT/lib"

# Add homebrew'd stuff to the manpath.
typeset -U manpath
manpath=($HOMEBREW_ROOT/share/man $manpath)

# Allow bundler to use all the cores for parallel installation
export BUNDLE_JOBS=4

# Make all the fancy `hub` shortcuts available via `git`.
alias git=hub

alias ql='qlmanage -p'
alias mcjpeg="$HOMEBREW_ROOT/opt/mozjpeg/bin/cjpeg"
alias mdjpeg="$HOMEBREW_ROOT/opt/mozjpeg/bin/djpeg"
alias mjpegtrans="$HOMEBREW_ROOT/opt/mozjpeg/bin/jpegtrans"

# Add Adobe Font Development Tools to PATH
export FDK_EXE="$HOME/bin/FDK/Tools/osx"

# The oh-my-zsh asdf plugin prepends the shim PATH automatically
export ASDF_FORCE_PREPEND=no # This probably isn't needed.

# Set PATH for non-arm64 systems
if [[ `uname -m` != "arm64" ]]; then
  path=(
    /Library/Apple/usr/bin
    /usr/local/opt/rustup/bin
    /usr/local/share/dotnet
    /usr/local/heroku/bin
    $HOMEBREW_ROOT/bin
    $HOMEBREW_ROOT/sbin
    /usr/bin
    /bin
    /usr/sbin
    /sbin
    /Library/Tex/texbin
    $HOME/bin/FDK/Tools/osx
    $HOME/Library/Python/3.7/bin
    $HOME/.docker/bin
    $HOME/bin
    $HOME/Code/go/bin
  )
fi

# TODO: See if there's a better way to set this up that still works with ElixirLS
#       As mentioned above, this seems redundant with the oh-my-zsh asdf plugin,
#       but that isn't getting loaded due to not loading oh-my-zsh in VSCode.
#       However, TERM_PROGRAM isn't set when ElixirLS runs launch.sh, so I don't
#       see a way to make this conditional. This doesn't actually hurt anything.
path=($HOME/.asdf/shims $path)

# Set PATH differently on arm64
if [[ `uname -m` == "arm64" ]]; then
  path=(
    $HOME/.asdf/shims
    /usr/local/opt/rustup/bin
    /usr/local/share/dotnet
    /usr/local/heroku/bin
    $HOMEBREW_ROOT/bin
    $HOMEBREW_ROOT/sbin
    /usr/local/bin
    /System/Cryptexes/App/usr/bin
    /usr/bin
    /bin
    /usr/sbin
    /sbin
    /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/local/bin
    /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/bin
    /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/appleinternal/bin
    /opt/pmk/env/global/bin
    /Library/Tex/texbin
    $HOME/bin/FDK/Tools/osx
    $HOME/Library/Python/3.7/bin
    $HOME/.docker/bin
    $HOME/bin
    $HOME/Code/go/bin
)

fi

export EDITOR=$HOMEBREW_ROOT/bin/vim

function view_applescript_interface_items_for {
  # Adapted from http://hints.macworld.com/article.php?story=20111208191312748
  # Useful for finding items that can be scripted with System Events in
  # AppleScript.
  osascript <<-eos | perl -pe '$_ =~ s/, /\n/g'
  set appname to "$1" -------------------------- Set this to the App you want to look at

  set winstuff to "defaultval"
  set menustuff to "defaultval"

  tell application appname
    activate
  end tell

  tell application "System Events"
    tell process appname
      set winstuff to entire contents of front window
      set menustuff to entire contents of menu bar 1
    end tell
  end tell
  return winstuff & menustuff -- comment this out to get just winstuff
  --return winstuff -- comment this out too to get just menustuff
  --return menustuff
eos
}

# Unlock the screen from the command line
# Adapted from: https://apple.stackexchange.com/a/230912
function unlock_screen {
  echo -n "Please enter your password: "
  read -s password
  osascript <<-eos
    tell application "System Events"
      key code 123 -- left arrow wakes the screen
      delay 0.8 -- wait for the login screen
      keystroke "$password" -- types the password
      key code 36 -- Enter - logs in
    end tell
eos
}

# Locking the screen is much easier from 10.13 onward.
# Remove this check and the extra code after upgrading.
autoload is-at-least
is-at-least "10.13" ${$(sw_vers -productVersion)%\.[0-9]}
if [[ $? -eq 1 ]] {
  # Lock the screen - Workaround since I haven't updated past Sierra yet
  # https://stackoverflow.com/a/41196453
  function lock_screen() {
    echo Locking screen please wait...
    PATH=/usr/bin:$PATH xcrun swift <<-eos >/dev/null 2>&1
    import Foundation

    CGEvent(mouseEventSource: nil, mouseType: .mouseMoved, mouseCursorPosition: CGPoint(x:30.0, y:0.0), mouseButton: .left)!.post(tap: CGEventTapLocation.cghidEventTap)
    CGEvent(mouseEventSource: nil, mouseType: .mouseMoved, mouseCursorPosition: CGPoint(x:0.0, y:0.0), mouseButton: .left)!.post(tap: CGEventTapLocation.cghidEventTap)
eos
  }
} else {
  # Lock the screen
  # https://apple.stackexchange.com/a/316058
  # Requires at least 10.13 (High Sierra)
  function lock_screen {
    osascript <<-eos
      activate application "SystemUIServer"
      tell application "System Events"
        tell process "SystemUIServer" to keystroke "q" using {command down, control down}
      end tell
eos
  }
}

# List Brew formula installed by user (installed_on_request)
function installed_on_request() {
  brew list --formula | xargs brew info --json | ruby -r json -ne 'j = JSON.parse($_); o = j.reduce([]) { |a, v| a << v["name"] unless v["installed"][0]["installed_on_request"].nil?; a }; puts o.join("\n")'
}
